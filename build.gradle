apply plugin: 'java'
apply plugin: 'jacoco'

sourceCompatibility = 1.8
version = '1.0'

repositories {
    maven {
        mavenCentral()
    }
}

sourceSets {
    integrationTest {
        java.srcDir file('src/integration/java')
        resources.srcDir file('src/integration/resources')
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    reports.html.enabled = false
}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/tests")
    reportOn test, integrationTest
}

test {
    reports.html.enabled = false
}

check.dependsOn integrationTest, testReport
integrationTest.mustRunAfter test
testReport.mustRunAfter integrationTest

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.+'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.+'
    compile group: 'commons-lang', name: 'commons-lang', version: '2.+'
    compile group: 'commons-collections', name: 'commons-collections', version: '3.+'
    compile group: 'commons-io', name: 'commons-io', version: '2.+'
    testCompile group: 'junit', name: 'junit', version: '4.+'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.+'
}
