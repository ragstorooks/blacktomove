apply plugin: 'java'
apply plugin: 'jacoco'

sourceCompatibility = 1.8
version = '1.0'

repositories {
    maven {
        mavenCentral()
        mavenLocal()
    }
}

sourceSets {
    integrationTest {
        java.srcDir file('src/integration/java')
        resources.srcDir file('src/integration/resources')
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    compile.exclude group: 'org.slf4j', module: 'slf4j-nop'
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    reports.html.enabled = false
}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/tests")
    reportOn test, integrationTest
}

test {
    reports.html.enabled = false
}

check.dependsOn integrationTest, testReport
integrationTest.mustRunAfter test
testReport.mustRunAfter integrationTest

dependencies {
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.3'
    compile group: 'commons-lang', name: 'commons-lang', version: '2.6'
    compile group: 'commons-collections', name: 'commons-collections', version: '3.2.1'
    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    compile group: 'com.datomic', name: 'datomic-free', version: '0.9.5173'
    compile group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.0.1'
    compile group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: '2.18'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.5.4'
    compile group: 'com.google.inject', name: 'guice', version: '4.0'
    testCompile group: 'junit', name: 'junit', version: '4.+'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
    testCompile group: 'org.glassfish.jersey.test-framework.providers', name:
    'jersey-test-framework-provider-grizzly2', version: '2.18'
}
